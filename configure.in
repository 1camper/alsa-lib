dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/control/control.c)
dnl *************************************************
dnl current:revision:age
dnl change (without API) = c:r+1:a
dnl change API = c+1:0:a
dnl add API = c+1:0:a+1
dnl remove API = c+1:0:0
dnl *************************************************
AM_INIT_AUTOMAKE(alsa-lib, 0.9.0beta10a)
eval LIBTOOL_VERSION_INFO="2:0:0"
dnl *************************************************
AM_CONDITIONAL(INSTALL_M4, test "x${ACLOCAL}" = "xaclocal")

AC_PREFIX_DEFAULT(/usr)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S 
AC_DISABLE_STATIC
AC_LIBTOOL_DLOPEN
AM_PROG_LIBTOOL

dnl path for sound/asound.h
AC_ARG_WITH(soundbase,
  [  --with-soundbase=dir    give the base directory with sound headers (optional)],
  [soundbasedir="$withval"], [soundbasedir=""])
if test "$soundbasedir" != "" -a -r "$soundbasedir/sound" ; then
  ALSA_CFLAGS="$ALSA_CFLAGS -I$soundbasedir"
  CFLAGS="$CFLAGS -I$soundbasedir"
fi
AC_MSG_RESULT($ALSA_CFLAGS)

dnl Checks for header files.
AC_HEADER_STDC
AM_CONFIG_HEADER(include/config.h)
# AC_CHECK_HEADERS(${soundbasedir}sound/asound.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_HEADER_TIME

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_CHECK_FUNC([hsearch_r], [HAVE_HSEARCH_R=yes])
AM_CONDITIONAL(ALSA_HSEARCH_R, [test "x$HAVE_HSEARCH_R" != xyes])

ALSA_CHECK_DRIVER
SAVE_LIBRARY_VERSION
AC_SUBST(LIBTOOL_VERSION_INFO)

test "x$prefix" = xNONE && prefix=$ac_default_prefix

eval dir="$datadir"
case "$dir" in
/*) ;;
*) dir="$PWD/$dir"
esac
AC_DEFINE_UNQUOTED(DATADIR, "$dir")

dnl Check for debug...
AC_MSG_CHECKING(for debug)
AC_ARG_WITH(debug,
  [  --with-debug=yes,no     library will (not) be compiled with asserts],
  debug="$withval", debug="yes")
if test "$debug" = "yes"; then
  AC_MSG_RESULT(yes)
else
  AC_DEFINE(NDEBUG)
  AC_MSG_RESULT(no)
fi

AC_OUTPUT(Makefile doc/Makefile include/Makefile src/Makefile \
          src/control/Makefile src/mixer/Makefile src/pcm/Makefile \
	  src/rawmidi/Makefile src/timer/Makefile \
          src/hwdep/Makefile src/seq/Makefile src/instr/Makefile \
          src/compat/Makefile src/conf/Makefile \
	  src/conf/cards/Makefile src/conf/pcm/Makefile \
	  aserver/Makefile test/Makefile utils/Makefile \
          utils/alsa-lib.spec)
