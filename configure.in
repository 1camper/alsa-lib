dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/control/control.c)
dnl *************************************************
dnl current:revision:age
dnl change (without API) = c:r+1:a
dnl change API = c+1:0:a
dnl add API = c+1:0:a+1
dnl remove API = c+1:0:0
dnl *************************************************
AM_INIT_AUTOMAKE(alsa-lib, 0.6.0pre1)
eval LIBTOOL_VERSION_INFO="2:0:0"
dnl *************************************************
AM_CONDITIONAL(INSTALL_M4, test "x${ACLOCAL}" = "xaclocal")

AC_PREFIX_DEFAULT(/usr)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S 
AM_PROG_LIBTOOL

dnl path for linux/asound.h
AC_ARG_WITH(kernel,
  [  --with-kernel=dir       give the directory with kernel sources (optional)],
  [kerneldir="$withval"], [kerneldir=""])
if test "$kerneldir" != "" -a -d $kerneldir/include ; then
  ALSA_CFLAGS="$ALSA_CFLAGS -I$kerneldir/include"
  CFLAGS="$CFLAGS -I$kerneldir/include"
fi
AC_MSG_RESULT($ALSA_CFLAGS)

dnl Checks for header files.
AC_HEADER_STDC
AM_CONFIG_HEADER(include/config.h)
# AC_CHECK_HEADERS(linux/asound.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_HEADER_TIME

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_CHECK_FUNC(hsearch_r, , [ COMPAT_SRCS="$COMPAT_SRCS hsearch_r.c" ])
AC_SUBST(COMPAT_SRCS)

ALSA_CHECK_DRIVER
SAVE_LIBRARY_VERSION
AC_SUBST(LIBTOOL_VERSION_INFO)

AC_OUTPUT(Makefile doc/Makefile include/Makefile src/Makefile \
          src/control/Makefile src/mixer/Makefile src/pcm/Makefile \
	  src/pcm/plugin/Makefile src/rawmidi/Makefile src/timer/Makefile \
          src/hwdep/Makefile src/seq/Makefile src/instr/Makefile \
          src/compat/Makefile src/conf/Makefile src/aserver/Makefile \
          test/Makefile utils/Makefile \
          utils/alsa-lib.spec)
